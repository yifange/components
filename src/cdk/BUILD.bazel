package(default_visibility = ["//visibility:public"])

load("//src/cdk:config.bzl", "CDK_ENTRYPOINTS", "CDK_ENTRYPOINTS_WITH_STYLES", "CDK_SCSS_LIBS", "CDK_TARGETS")
load("//tools:defaults.bzl", "ng_package", "ts_library")

ts_library(
    name = "cdk",
    srcs = glob(
        ["*.ts"],
        exclude = ["**/*.spec.ts"],
    ),
    module_name = "@angular/cdk",
    deps = ["@npm//@angular/core"],
)

# List of style files that need to be copied to the root of the CDK package. We do this
# to make it easier for developers to import these styles without needing to know about
# deep imports in the release output. Note that this is done for backwards compatibility
# with the Gulp release output. TODO(devversion): consider removing this in the future.
rerootedStyles = [file for target in CDK_ENTRYPOINTS_WITH_STYLES for file in [
    [
        "_%s.scss" % target,
        target,
    ],
    [
        "%s-prebuilt.css" % target,
        target,
    ],
]]

# Create genrules which re-root stylesheets from secondary entry-points.
[genrule(
    name = "%s_rerooted" % file,
    srcs = ["//src/cdk/%s:%s" % (target, file)],
    outs = [file],
    cmd = "cp $< $@",
) for [
    file,
    target,
] in rerootedStyles]

# List of targets which generate the re-rooted stylesheet files.
rerootedStyleTargets = ["%s_rerooted" % file for [
    file,
    _,
] in rerootedStyles]

# Creates the @angular/cdk package published to npm.
ng_package(
    name = "npm_package",
    srcs = ["package.json"],
    data = rerootedStyleTargets + CDK_SCSS_LIBS,
    entry_point = ":public-api.ts",
    nested_packages = [
        "//src/cdk/schematics:npm_package",
    ],
    tags = ["release-package"],
    deps = CDK_TARGETS,
)

filegroup(
    name = "overviews",
    # Only secondary entry-points declare overview files currently. Entry-points
    # which contain a slash are not in the top-level and do not have an overview.
    srcs = ["//src/cdk/%s:overview" % ep for ep in CDK_ENTRYPOINTS if not "/" in ep],
)
