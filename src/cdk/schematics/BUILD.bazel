package(default_visibility = ["//visibility:public"])

load("@build_bazel_rules_nodejs//:index.bzl", "pkg_npm")
load("//:packages.bzl", "VERSION_PLACEHOLDER_REPLACEMENTS")
load("//tools:defaults.bzl", "jasmine_node_test", "ts_library")

filegroup(
    name = "schematics_assets",
    srcs = glob([
        "**/files/**/*",
        "**/*.json",
    ]),
)

ts_library(
    name = "schematics",
    srcs = glob(
        ["**/*.ts"],
        exclude = [
            "**/files/**/*.ts",
            "**/*.spec.ts",
            "ng-update/test-cases/**/*.ts",
            "testing/**/*.ts",
        ],
    ),
    module_name = "@angular/cdk/schematics",
    tsconfig = ":tsconfig.json",
    deps = [
        "//src/cdk/schematics/update-tool",
        "@npm//@schematics/angular",
        "@npm//@angular-devkit/core",
        "@npm//@angular-devkit/schematics",
        # TODO(devversion): Only include jasmine for test sources (See: tsconfig types).
        "@npm//@types/jasmine",
        "@npm//@types/node",
        "@npm//glob",
        "@npm//parse5",
        "@npm//typescript",
    ],
)

# This package is intended to be combined into the main @angular/cdk package as a dep.
pkg_npm(
    name = "npm_package",
    srcs = [":schematics_assets"],
    substitutions = VERSION_PLACEHOLDER_REPLACEMENTS,
    deps = [":schematics"],
)

### Testing rules

jasmine_node_test(
    name = "unit_tests",
    srcs = [":unit_test_sources"],
    data = [
        ":schematics_assets",
        ":schematics_test_cases",
    ],
)

ts_library(
    name = "unit_test_sources",
    testonly = True,
    srcs = glob(
        ["**/*.spec.ts"],
        exclude = [
            "**/*.e2e.spec.ts",
            "**/files/**/*.spec.ts",
        ],
    ),
    tsconfig = ":tsconfig.json",
    deps = [
        ":schematics",
        "//src/cdk/schematics/testing",
        "//src/cdk/schematics/update-tool",
        "@npm//@angular-devkit/schematics",
        "@npm//@schematics/angular",
        "@npm//@types/jasmine",
        "@npm//@types/node",
        "@npm//typescript",
    ],
)

filegroup(
    name = "schematics_test_cases",
    testonly = True,
    srcs = glob([
        "ng-update/test-cases/**/*_input.ts",
        "ng-update/test-cases/**/*_expected_output.ts",
    ]) + ["ng-update/test-cases/misc/global-stylesheets-test.scss"],
)
